apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
version = 'v0.6.1'

mainClassName = "at.fhtw.mcs.Main"

applicationDefaultJvmArgs = ["-Xmx2g"]

repositories {
    mavenCentral()
}

dependencies {
	compile 'javazoom:jlayer:1.0.1'
	compile 'commons-io:commons-io:2.4'
	compile 'org.jsoup:jsoup:1.8.1'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

import org.apache.tools.ant.filters.*

processResources {
    filter ReplaceTokens, tokens: [
        "version": project.property("version")
    ]
}

// To get more test output from Travis
test {
    testLogging {
        exceptionFormat "full" // default is "short"
    }
}

//
// Sonarqube plugin for static code analysis.
// Requires a running local instance of Sonarqube. Download:
//   http://www.sonarqube.org/downloads/
// Extract, run (e.g. bin/linux-x86-64/sonar.sh start)
//
apply plugin: "org.sonarqube"

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.sonarqube.gradle:gradle-sonarqube-plugin:1.1"
  }
}

//for creating DMG file
apply plugin: "edu.sc.seis.macAppBundle"

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.edu.sc.seis:macAppBundle:2.1.5"
  }
}

macAppBundle {
    mainClassName = "at.fhtw.mcs.Main"
}

// include license and README in distribution package
distZip {
    into(project.name + "-" + version) {
        from '.'
        include 'LICENSE.txt'
        include 'LICENSE-3RD-PARTY.txt'
    }

    // README.md is renamed to README.txt and Images are filtered
    into(project.name + "-" + version) {
    	from '.'
    	include 'README.md'

    	filter { String line ->
	    line.replaceAll('!?\\[.*\\]\\(.*\\)', '')
    	}

    	rename { String fileName ->
    	    fileName.replace('.md', '.txt')
    	}
    }
}
